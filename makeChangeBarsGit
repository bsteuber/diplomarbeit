#!/bin/tcsh -f
#
# Compares a TeX file with a given old GIT revision
# and marks the changed lines with commands from the
# changebars latex package

if ( $# < 2 || $# > 3 ) then
    echo "Usage: `basename ${0}` [-g] texfile version"
    echo "       -g: debug mode, -G: more debug mode"
    exit 1
endif

set debug=
if ( "${1}" =~ "-g" ) then
    echo "`basename ${0}`: Debug mode on"
    set debug="-g"
    shift
else if ( "${1}" =~ "-G" ) then
    echo "${0}: More debug mode on"
    set debug="-G"
    shift
endif

if ( $# != 2 ) then
    echo "Usage: `basename ${0}` [-g] texfile version"
    echo "       -g: debug mode, -G: more debug mode"
    exit 1
endif

if ( ! -f "$1" ) then
    echo "${0}: ${1} does not exist."
    exit 1
else if ( ! -d cb ) then
    echo "${0}: need subdirectory ./cb to put the modified files into"
    exit 1
else if ( ! -d .git ) then
    echo "${0}: there is no GIT information here"
    exit 1
endif

set file="${1}"
set rev="${2}"
set visitFile="${file}.~${rev}~"
echo "Retrieving revision ${rev} from ${file}"
echo "           into ${visitFile}"
set truerev=`(git ls-tree "${rev}" "${file}) | cut -d " " -f 3`
if ( "${debug}" == "-G" ) then
    echo '%% truerev='"${truerev}"
endif
if ( "${debug}" =~ -[Gg] ) then
    echo '> git show '"${rev}"':'"${file}"' > '"${visitFile}"
endif
git show "${rev}":"${file}" > "${visitFile}"
if ( `wc -c "${visitFile}" | cut -b 1-8` == 0 ) then
    echo "           File is empty. Dead revision?"
endif
if ( "$truerev" !~ "$rev" ) then
    set trueVisitFile="${file}.~${truerev}~"
    echo "           Renaming to ${trueVisitFile}"
    if ( "${debug}" =~ -[Gg] ) then
	echo '> mv -f '"${visitFile}" "${trueVisitFile}"
    endif
    mv -f "${visitFile}" "${trueVisitFile}"
    set visitFile="${trueVisitFile}"
    if ( "${debug}" == "-G" ) then
	echo '%% visitFile='"${visitFile}"
    endif
endif

echo "           Marking changes against ${file} in cb/${file}"
if ( "${debug}" =~ -[Gg] ) then
    echo '> latexdiff -V '"${visitFile}" "${file}"' > '"cb/${file}"
endif
latexdiff  -V "${visitFile}" "${file}" > "cb/${file}"
exit 0
